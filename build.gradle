import org.apache.tools.ant.filters.ReplaceTokens

plugins {
  id 'groovy'
  id 'java-gradle-plugin'
  id 'com.gradle.plugin-publish' version '2.0.0'
  id "net.researchgate.release" version "3.1.0"
}

group = 'de.schablinski.gradle.activejdbc'
description = """Gradle plugin for instrumenting ActiveJDBC model classes."""

def activeJdbcVersion = '2.6-j8'
def slf4JSimpleVersion = '1.7.32'

repositories {

  mavenCentral()
  
}

ext {
  
}

if (project.hasProperty("testResultsDir")) {
  java.testResultsDir = layout.buildDirectory.dir("${project.property('testResultsDir')}")
}
if (project.hasProperty("reportingDir")) {
  reporting.baseDirectory = file(project.property('reportingDir'))
}

tasks.withType(JavaCompile).configureEach {
  options.release = 17
}

dependencies {
  implementation localGroovy()

  implementation "org.javalite:activejdbc-instrumentation:${activeJdbcVersion}"

  // Debugging your plugin code with Gradle TestKit: set system property org.gradle.testkit.debug to true in the unit test's run configuration of your IDE
  testImplementation gradleTestKit()

  testImplementation 'org.junit.jupiter:junit-jupiter:5.14.1'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  testImplementation ('org.spockframework:spock-core:2.4-M6-groovy-4.0') {
    exclude group: 'org.apache.groovy', module: 'groovy'
  }
  testImplementation "org.apache.logging.log4j:log4j-api:2.25.2", "org.apache.logging.log4j:log4j-core:2.25.2"
}

tasks.register("processVersionFile", WriteProperties) {
  destinationFile = layout.buildDirectory.file("resources/main/activejdbc-gradle-plugin.properties")

  property 'activejdbc-version', activeJdbcVersion
  property 'slf4j-simple-version', slf4JSimpleVersion
}
tasks.classes.dependsOn processVersionFile

processTestResources {
  filter ReplaceTokens, tokens: ["gradle-test-kit-dir" : layout.buildDirectory.dir("tmp").get().getAsFile().absolutePath.replace('\\', '/')]
}

def docJar = tasks.register("docJar", Jar) {
    archiveClassifier.set('javadoc')
    from 'build/docs/groovydoc'
}
docJar.configure {
  dependsOn groovydoc
}

def sourceJar = tasks.register("sourceJar", Jar) {
  archiveClassifier.set('sources')
  from sourceSets.main.allSource
}

afterEvaluate {
  tasks.named('generateMetadataFileForPluginMavenPublication') {
    dependsOn sourceJar, docJar
  }
}

def createPluginClasspath = tasks.register("createPluginClasspath") {
  Provider<Directory> outputDir = layout.buildDirectory.dir("resources/test")

  inputs.files sourceSets.test.runtimeClasspath
  outputs.dir outputDir

  doLast {
    outputDir.get().getAsFile().mkdirs()
    outputDir.get()
            .file("plugin-classpath.txt")
            .getAsFile().text = sourceSets.test.runtimeClasspath.join('\n',)
  }
}

test {
  useJUnitPlatform()
}

test.dependsOn << createPluginClasspath

jar {
  manifest {
    attributes("Name": project.group + '.' + rootProject.name,
            "Implementation-Title": project.description,
            "Build-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "Implementation-Version":  project.version)
  }
}

gradlePlugin {
  website = 'https://github.com/cschabl/activejdbc-gradle-plugin'
  vcsUrl = 'https://github.com/cschabl/activejdbc-gradle-plugin.git'
  plugins {
    activeJdbcPlugin {
      id = 'de.schablinski.activejdbc-gradle-plugin'
      displayName = 'ActiveJDBC instrumentation plugin'
      description = 'A plugin to instrument your project\'s model classes for the ActiveJDBC ORM framework.'
      implementationClass = 'de.schablinski.gradle.activejdbc.ActiveJDBCGradlePlugin'
      tags.set(['activejdbc'])
    }
  }
}

assemble.dependsOn += [ 'sourceJar', 'docJar']


